#!/usr/bin/perl

use strict;
use warnings;
use lib './lib';

use English qw(-no_match_vars);
use Getopt::Long;
use Pod::Usage;
use XML::TreePP;

use FusionInventory::Agent::Task::NetInventory;
use FusionInventory::Agent::Tools;
use FusionInventory::Agent::Logger;
use FusionInventory::Agent::Target;

my %types = (
    1 => 'COMPUTER',
    2 => 'NETWORKING',
    3 => 'PRINTER',
    4 => 'STORAGE',
    5 => 'POWER',
    6 => 'PHONE',
    7 => 'VIDEO',
);

my $options = {
    debug => 0
};

my %setup = (
    confdir => './etc',
    datadir => './share',
    libdir  => './lib',
    vardir  => './var',
);

GetOptions(
    $options,
    'server=s',
    'directory=s',
    'type=s',
    'host=s',
    'file=s',
    'community=s',
    'credentials=s',
    'entity=s',
    'timeout=i',
    'debug+',
    'help',
) or pod2usage(-verbose => 0);

pod2usage(-verbose => 0, -exitval => 0) if $options->{help};

pod2usage(
    -message => "--host option is deprecated, use regular argument instead\n",
    -verbose => 0
) if $options->{host};

pod2usage(
    -message => "--file option is deprecated, use regular argument instead\n",
    -verbose => 0
) if $options->{file};

pod2usage(
    -message => "--community option is deprecated, use --credentials instead\n",
    -verbose => 0
) if $options->{community};

pod2usage(
    -message => "no host given, aborting\n", -verbose => 0
) unless @ARGV;

pod2usage(
    -message => "invalid type '$options->{type}', aborting\n",
    -verbose => 0
) if $options->{type} && none { $options->{type} eq $_ } values %types;

my $host_id = 0;
my @hosts;
foreach my $arg (@ARGV) {
    my $host = {
        authsnmp_id => 1,
    };
    foreach my $parameter (split(',', $arg)) {
        my ($key, $value) = split(':', $parameter);
        if (!$value) {
            $host->{ip} = $key;
        } else {
            $host->{$key} = $value;
        }
    }
    $host->{id}     = $host_id++ unless $host->{id};
    $host->{entity} = $options->{entity} if $options->{entity};
    $host->{type}   = $options->{type} if $options->{type};
    push @hosts, $host;
}

my $credentials = { id => 1 };

if ($options->{credentials}) {
    foreach my $parameter (split(',', $options->{credentials})) {
        my ($key, $value) = split(':', $parameter);
        $credentials->{$key} = $value;
    }
} else {
    $credentials->{community} = 'public';
}

my $verbosity =
    $options->{debug} == 0 ? LOG_INFO   :
    $options->{debug} == 1 ? LOG_DEBUG  :
    $options->{debug} == 2 ? LOG_DEBUG2 :
                             LOG_DEBUG2 ;

my $logger = FusionInventory::Agent::Logger->new(verbosity => $verbosity);

my $task = FusionInventory::Agent::Task::NetInventory->new(
    logger   => $logger,
    datadir  => $setup{datadir},
    confdir  => $setup{confdir},
    deviceid => 'foo',
);

$task->configure(
    pid         => 1,
    timeout     => $options->{timeout},
    devices     => \@hosts,
    credentials => [ $credentials ]
);

my $target = FusionInventory::Agent::Target->create(
    url    => $options->{server},
    path   => $options->{directory},
    logger => $logger,
);

$task->run(target => $target);

__END__

=head1 NAME

fusioninventory-netinventory - Standalone network inventory

=head1 SYNOPSIS

fusioninventory-netinventory [options] <host>

  Options:
    --directory    directory target
    --server       server target
    --credentials  SNMP credentials
    --timeout val  SNMP timeout (default: 15s)
    --entity       GLPI entity
    --debug        debug output (execution traces)
    -h --help      print this message and exit

=head1 DESCRIPTION

F<fusioninventory-netinventory> allows to run a network inventory task without
a GLPI server.
