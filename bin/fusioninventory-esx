#!/usr/bin/perl

use strict;
use warnings;
use lib './lib';

use English qw(-no_match_vars) ;
use Getopt::Long;
use Pod::Usage;

use FusionInventory::Agent;
use FusionInventory::Agent::Config;
use FusionInventory::Agent::Logger;
use FusionInventory::Agent::Target;
use FusionInventory::Agent::Task::ESX;

my %setup = (
    confdir => './etc',
    datadir => './share',
    libdir  => './lib',
    vardir  => './var',
);

my $options = {
    'workers'   => 0,
    'conf-file' => $setup{confdir} . '/agent.cfg',
};

GetOptions(
    $options,
    'config=s',
    'conf-file=s',
    'target=s',
    'user=s',
    'password=s',
    'tag=s',
    'workers=i',
    'logger-verbosity=s',
    'help',
    # deprecated
    'debug+',
    'directory=s',
    'host=s',
) or pod2usage(-verbose => 0);

pod2usage(-verbose => 0, -exitstatus => 0) if $options->{help};

pod2usage(
    -message => "--host option is deprecated, use regular argument instead\n",
    -verbose => 0
) if $options->{host};

pod2usage(
    -message => "--directory option is deprecated, use --target instead\n",
    -verbose => 0
) if $options->{directory};

pod2usage(
    -message => "no host given, aborting\n", -verbose => 0
) unless @ARGV;

my $config = FusionInventory::Agent::Config->create(
    backend   => $options->{'config'},
    file      => $options->{'conf-file'},
    options   => {
        _ => {
            tag => $options->{tag},
        },
        logger => {
            verbosity => $options->{'logger-verbosity'}
        }
    }
);

my $logger = FusionInventory::Agent::Logger->new(
    backends  => $config->{logger}->{backends},
    file      => $config->{logger}->{file},
    maxsize   => $config->{logger}->{maxsize},
    facility  => $config->{logger}->{facility},
    verbosity => $config->{logger}->{verbosity},
);

my $agent = FusionInventory::Agent->new(
    config => $config,
    logger => $logger,
    setup  => \%setup
);
$agent->init();
$agent->initHandlers();

my $task = FusionInventory::Agent::Task::ESX->new(
    logger => $logger,
);

my @jobs;
foreach my $arg (@ARGV) {
    my $job;
    foreach my $parameter (split(',', $arg)) {
        my ($key, $value) = split(':', $parameter);
        if (!$value) {
            $job->{host} = $key;
        } else {
            $job->{$key} = $value;
        }
    }
    $job->{user}     = $options->{user}     if $options->{user};
    $job->{password} = $options->{password} if $options->{password};
    push @jobs, $job;
}

$task->configure(
    workers => $options->{workers},
    jobs    => \@jobs
);

my $target = FusionInventory::Agent::Target->create(
    spec   => $options->{target},
    logger => $logger,
    config => $config,
);

$agent->executeTask(task => $task, target => $target);

__END__

=head1 NAME

fusioninventory-esx - Standalone ESX inventory

=head1 SYNOPSIS

B<fusioninventory-esx>
S<[B<--config> I<string>]>
S<[B<--conf-file> I<path>]>
S<[B<--target> I<string>]>
S<[B<--tag> I<string>]>
S<[B<--user> I<string>]> S<[B<--password> I<string>]>
S<[B<--workers> I<integer>]>
S<[B<--logger-verbosity> I<string>]>
I<host> S<[I<host> ...]>

=head1 DESCRIPTION

F<fusioninventory-esx> allows to run an ESX inventory task without
a GLPI server.

=head1 OPTIONS

=over

=item B<--config> I<string>

Configuration backend to use (default: file).

The available backends are:

=over 4

=item

file: read configuration from a file

=item

registry: read configuration from the registry

=item

none: don't read any configuration.

=back

=item B<--conf-file> I<path>

configuration file path (with file backend)

=item B<--target> I<string>

target path (for directory) or url (for server)

=item B<--user> I<string>

ESX user name

=item B<--password> I<string>

ESX user password

=item B<--workers> I<integer>

worker processes number (default: 0)

=item B<--logger-verbosity> I<string>

Set B<logger/verbosity> option, see L<agent.cfg> for details.

=item B<--tag> I<string>

Set B<tag> option, see L<agent.cfg> for details.

=back

=head1 EXAMPLES

Execute an inventory of a single ESX host, sending the results to stdout:

    $> fusioninventory-esx --user foo --password bar esx1

Execute an inventory of a single ESX host, sending the results to a directory:

    $> fusioninventory-esx --user foo --password bar --directory /tmp esx1

Execute an inventory of a single ESX host, sending the results to a server

    $> fusioninventory-esx --user foo --password bar \
        --server http://glpi.domain.com/plugins/fusioninventory esx1

Execute an inventory of two ESX hosts, using same credentials, sending the
results to stdout:

    $> fusioninventory-esx --user foo --password bar esx1 esx2

Execute an inventory of two ESX hosts, using different credentials, sending the
results to stdout:

    $> fusioninventory-esx host:esx1,user:foo,password:bar
