#!/usr/bin/perl

use strict;
use warnings;
use lib './lib';

use English qw(-no_match_vars) ;
use Getopt::Long;
use Pod::Usage;

use FusionInventory::Agent::Config;
use FusionInventory::Agent::Logger;
use FusionInventory::Agent::Target;
use FusionInventory::Agent::Task::ESX;

my %setup = (
    confdir => './etc',
    datadir => './share',
    libdir  => './lib',
    vardir  => './var',
);

my $options = {
    debug => 0,
};

GetOptions(
    $options,
    'host=s',
    'user=s',
    'password=s',
    'directory=s',
    'server=s',
    'tag=s',
    'debug+',
    'help',
) or pod2usage(-verbose => 0);

pod2usage(-verbose => 0, -exitstatus => 0) if $options->{help};

pod2usage(-verbose => 0) unless
    $options->{host}      and
    $options->{user}      and
    $options->{password};

my $config = FusionInventory::Agent::Config->new(
    confdir => $setup{confdir},
    options => $options,
);

my $logger = FusionInventory::Agent::Logger->new(
    config => $config
);

my $task = FusionInventory::Agent::Task::ESX->new(
    logger => $logger,
    datadir => $setup{datadir},
    confdir => $setup{confdir},
);

$task->configure(
    jobs => [
        {
            host     => $options->{host},
            user     => $options->{user},
            password => $options->{password},
        }
    ]
);

my $target = FusionInventory::Agent::Target->create(
    url    => $options->{server},
    path   => $options->{directory},
    logger => $logger,
);

$task->run(target => $target);

__END__

=head1 NAME

fusioninventory-esx - Standalone ESX inventory

=head1 SYNOPSIS

B<fusioninventory-esx>
S<[B<--tag> I<string>]> S<[B<--host> I<string>]>
S<[B<--user> I<string>]> S<[B<--password> I<string>]>
S<[B<--directory> I<path>]> S<[B<--server> I<url>]>
S<[B<--debug>]>

=head1 DESCRIPTION

F<fusioninventory-esx> allows to run an ESX inventory task without
a GLPI server.

=head1 OPTIONS

=over

=item B<--tag> I<string>

inventory tag

=item B<--host> I<string>

ESX server hostname

=item B<--user> I<string>

ESX user name

=item B<--password> I<string>

ESX user password

=item B<--directory> I<path>

directory target path

=item B<--server> I<url>

server target url

=item B<--debug>

debug output

=back

=head1 EXAMPLES

    $> fusioninventory-esx --user foo --password bar --host esx1
    $> fusioninventory-esx --user foo --password bar --host esx1
        --directory /tmp
    $> fusioninventory-esx --user foo --password bar --host esx1
        --server http://glpi.domain.com/plugins/fusioninventory
