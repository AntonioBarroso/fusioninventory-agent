#!/usr/bin/perl

use strict;
use warnings;
use lib './lib';

use English qw(-no_match_vars);
use Getopt::Long;
use Pod::Usage;

use FusionInventory::Agent;
use FusionInventory::Agent::Config;
use FusionInventory::Agent::Logger;
use FusionInventory::Agent::Target;
use FusionInventory::Agent::Task::NetDiscovery;

my $options = {
    debug   => 0,
    threads => 1
};

my %setup = (
    confdir => './etc',
    datadir => './share',
    libdir  => './lib',
    vardir  => './var',
);

GetOptions(
    $options,
    'config=s',
    'conf-file=s',
    'target=s',
    'credentials=s@',
    'entity=s',
    'threads=i',
    'timeout=i',
    'debug+',
    'help',
    # deprecated
    'community=s@',
    'credential=s',
    'first=s',
    'last=s',
) or pod2usage(-verbose => 0);

pod2usage(-verbose => 0, -exitval => 0) if $options->{help};

pod2usage(
    -message => "--first option is deprecated, use regular argument instead\n",
    -verbose => 0
) if $options->{first};

pod2usage(
    -message => "--last option is deprecated, use regular argument instead\n",
    -verbose => 0
) if $options->{last};

pod2usage(
    -message => "--community option is deprecated, use --credentials instead\n",
    -verbose => 0
) if $options->{community};

pod2usage(
    -message => "--credential option has been renamed to --credentials\n",
    -verbose => 0
) if $options->{credential};

pod2usage(
    -message => "no network given, aborting\n", -verbose => 0
) unless @ARGV;

my $config = FusionInventory::Agent::Config->new(
    confdir => $setup{confdir},
    options => $options,
);

my $logger = FusionInventory::Agent::Logger->new(
    config => $config
);

my $agent = FusionInventory::Agent->new(
    config => $config,
    logger => $logger,
    setup  => \%setup
);
$agent->init();
$agent->initHandlers();

my $task = FusionInventory::Agent::Task::NetDiscovery->new(
    logger => $logger,
    name   => 'NetDiscovery'
);

my $block_id = 1;
my @blocks;
foreach my $arg (@ARGV) {
    my $block;
    foreach my $parameter (split(',', $arg)) {
        my ($key, $value) = split(':', $parameter);
        if (!$value) {
            $block->{spec} = $key;
        } else {
            $block->{$key} = $value;
        }
    }
    $block->{id}     = $block_id++ unless $block->{id};
    $block->{entity} = $options->{entity} if $options->{entity};
    push @blocks, $block;
}

my $credentials_id = 1;
my @credentials;
if ($options->{credentials}) {
    foreach my $specification (@{$options->{credentials}}) {
        my $credentials = { id => $credentials_id++ };
        foreach my $parameter (split(',', $specification)) {
            my ($key, $value) = split(':', $parameter);
            $credentials->{$key} = $value;
        }
        push @credentials, $credentials;
    }
} else {
    push @credentials, {
        id => $credentials_id++, version => '2c', community => 'public'
    };
}

$task->configure(
    pid         => 1,
    threads     => $options->{threads},
    timeout     => $options->{timeout},
    blocks      => \@blocks,
    snmp_credentials => \@credentials,
);

my $target = FusionInventory::Agent::Target->create(
    spec   => $options->{target},
    logger => $logger,
    config => $config,
);

$agent->executeTask(task => $task, target => $target);

__END__

=head1 NAME

fusioninventory-netdiscovery - Standalone network discovery

=head1 SYNOPSIS

B<fusioninventory-netdiscovery>
S<[B<--config> I<string>]>
S<[B<--conf-file> I<path>]>
S<[B<--target> I<string>]>
S<[B<--credentials> I<string>]> S<[B<--timeout> I<integer>]>
S<[B<--threads> I<integer>]> S<[B<--entity> I<string>]>
S<[B<--debug>]>
I<network> S<[I<network> ...]>

=head1 DESCRIPTION

F<fusioninventory-netdiscovery> allows to run a network discovery task without a
GLPI server.

=head1 OPTIONS

=over

=item B<--config> I<string>

Configuration backend to use.

The available backends are:

=over 4

=item

file: read configuration from a file (default anywhere else as Windows).

=item

registry: read configuration from the registry (default on Windows).

=item

none: don't read any configuration.

=back

=item B<--conf-file> I<path>

Use I<path> as configuration file (implies file configuration backend).

=item B<--target> I<string>

target path (for directory) or url (for server)

=item B<--credentials> I<string>

SNMP credentials (default: version:2c,community:public)

=item B<--timeout> I<integer>

SNMP timeout (default: 1s)

=item B<--threads> I<integer>

threads number (default: 1)

=item B<--entity> I<string>

GLPI entity

=item B<--debug>

debug output

=back

=head1 EXAMPLES

Execute a discovery of a single network, sending the results to stdout:

    $> fusioninventory-netdiscovery 192.168.1.0/24

Execute a discovery of a single network, sending the results to a directory:

    $> fusioninventory-netdiscovery --directory /tmp 192.168.1.0/24

Execute a discovery of a single network, sending the results to a server:

    $> fusioninventory-netdiscovery \
        --server http://glpi.domain.com/plugins/fusioninventory/ \
        192.168.1.0/24

Execute a discovery of a single network, using two set of SNMP credentials,
sending the results to stdout:

    $> fusioninventory-netdiscovery \
        --credentials version:1,community:public \
        --credentials \
        version:3,username:foo,authprotocol:sha1,authpassword:bar \
        192.168.1.0/24

Execute a discovery of two networks, sending the results to stdout:

    $> fusioninventory-netdiscovery 192.168.1.0/24 192.168.2.0/24

Execute a discovery of a single network, using firs-last syntax, sending the
results to stdout:

    $> fusioninventory-netdiscovery 192.168.1.1-192.168.1.254

