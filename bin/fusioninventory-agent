#!/usr/bin/perl

use strict;
use warnings;

use lib './lib';
use setup;

use English qw(-no_match_vars);
use Getopt::Long;
use Pod::Usage;

use FusionInventory::Agent;

Getopt::Long::Configure( "no_ignorecase" );

my $options = {};

GetOptions(
    $options,
    'config-file=s',
    'config-reload-interval=i',
    'config-backend=s',
    'daemon|d',
    'debug+',
    'help|h',
    'list-modules',
    'logger-backend=s',
    'logger-file=s',
    'logger-maxsize=i',
    'no-module=s',
    'no-fork',
    'pidfile=s',
    'listener-disable',
    'listener-ip=s',
    'listener-port=s',
    'listener-trust=s',
    'server-timeout=i',
    'server-user|u=s',
    'server-url|s=s',
    'server-password|p=s',
    'server-ca-cert-path=s',
    'server-no-ssl-check',
    'server-proxy|P=s',
    'setup',
    'tag|t=s',
    'version',
    'wait|w=s',
    # deprecated options
    'additional-content=s',
    'backend-collect-timeout=s',
    'ca-cert-dir=s',
    'ca-cert-file=s',
    'color',
    'delaytime=s',
    'force|f',
    'html',
    'lazy',
    'list-tasks',
    'local',
    'no-category=s',
    'no-compression|C',
    'no-task',
    'no-p2p',
    'scan-homedirs',
    'scan-profiles',
    'tasks',
    # Platform specific option
    'no-win32-ole-workaround'
) or pod2usage(-verbose => 0);

pod2usage(-verbose => 0, -exitstatus => 0) if $options->{help};

if ($options->{version}) {
    my $PROVIDER = $FusionInventory::Agent::Version::PROVIDER;
    print $_ . "\n" foreach (
        "$PROVIDER Agent $FusionInventory::Agent::VERSION",
        @{$FusionInventory::Agent::COMMENTS}
    );
    exit 0;
}

if ($options->{setup}) {
    foreach my $key (keys %setup) {
        print "$key: $setup{$key}\n";
    }
    exit 0;
}

if ($options->{'config-file'}) {
    if ($options->{'config-backend'}) {
        if ($options->{'config-backend'} ne 'file') {
            print STDERR
                "don't use --config-file with $options->{config} backend";
            exit 1;
        }
    } else {
        $options->{'config-backend'} = 'file';
    }
}

if ($OSNAME eq 'MSWin32' && ! $options->{'no-win32-ole-workaround'}) {
    # From here we may need to avoid crashes due to not thread-safe Win32::OLE
    FusionInventory::Agent::Tools::Win32->require();
    FusionInventory::Agent::Tools::Win32::start_Win32_OLE_Worker();
}

my $agent = FusionInventory::Agent->new(%setup);

if ($options->{'list-modules'}) {
    my %tasks = $agent->getAvailableTasks();
    print "Available modules: \n";
    foreach my $task (keys %tasks) {
        print "$task (v$tasks{$task})\n";
    }

    exit 0;
}

if ($options->{wait}) {
    my $time = int rand($options->{wait});
    sleep $time;
}

eval {
    $agent->init(
        options => {
            _ => {
                no_module => $options->{'no-module'},
                tag       => $options->{'tag'},
            },
            server => {
                url          => $options->{'server-url'},
                ca_cert_path => $options->{'server-ca-cert-path'},
                no_ssl_check => $options->{'server-no-ssl-check'},
                password     => $options->{'server-password'},
                proxy        => $options->{'server-proxy'},
                timeout      => $options->{'server-timeout'},
                user         => $options->{'server-user'},
            },
            listener => {
                disable      => $options->{'listener-disable'},
                ip           => $options->{'listener-ip'},
                port         => $options->{'listener-port'},
                trust        => $options->{'listener-trust'},
            },
            logger => {
                backend      => $options->{'logger-backend'},
                debug        => $options->{'logger-debug'},
                file         => $options->{'logger-file'},
                maxsize      => $options->{'logger-maxsize'},
                facility     => $options->{'logger-facility'},
            }
        }
    );
    $agent->run();
};

if ($EVAL_ERROR) {
    print STDERR "Execution failure:.\n";
    print STDERR $EVAL_ERROR;
    exit 1;
}

exit(0);

__END__

=head1 NAME

fusioninventory-agent - FusionInventory agent For Linux/UNIX, Windows and MacOSX

=head1 SYNOPSIS

B<fusioninventory-agent> [options] [--server server]

  Module selection options:
    --list-modules                     list available modules and exit
    --no-module=MODULE[,MODULE]...     do not run given module

  Server options:
    -s --server-url=URL            control server URL
    -P --server-proxy=URL          proxy URL
    -u --server-user=USER          user name for server authentication
    -p --server-password=PASSWORD  password for server authentication
    --server-ca-cert-path=PATH     CA certificates path
    --server-no-ssl-check          do not check server SSL certificate (false)
    --server-timeout=TIME          connection timeout, in seconds (180)

  Web interface options:
    --listener-disable             disable embedded web server (false)
    --listener-ip=IP               network interface to listen to (all)
    --listener-port=PORT           network port to listen to (62354)
    --listener-trust=IP            trusted networks for requests

  Logger options:
    --logger-backend=BACKEND       logger backend (stderr)
    --logger-file=FILE             log file
    --logger-maxsize=SIZE          log file maximum size in MB (0)
    --logger-facility=FACILITY     syslog facility (LOG_USER)

  Configuration options:
    --config-backend=BACKEND       configuration backend
    --config-file=FILE             configuration file
    --config-reload-interval=TIME  time between configuration reloading

  Execution mode options:
    -w --wait=LIMIT                maximum delay before execution,
                                     in seconds
    -d --daemon                    run the agent as a daemon (false)
    --no-fork                      don't fork in background (false)
    -t --tag=TAG                   add given tag to inventory results
    --debug                        debug mode (false)
    --setup                        print the agent setup directories
                                     and exit
    --version                      print the version and exit
    --no-win32-ole-workaround      [win32 only] disable win32 work-around
                                     used to better handle Win32::OLE apis.
                                     !!! Use it at your own risk as you may
                                     experiment perl crash under win32 !!!

=head1 DESCRIPTION

The F<fusioninventory-agent> agent is a generic multi-platform agent. It can
perform a large array of management tasks, such as local inventory, software
deployment or network discovery. It can be used either standalone, or in
combination with a compatible server (OCS, GLPI, OTRS) acting as a centralized
control point.

=head1 OPTIONS

Most of the options are available in a I<short> form and a I<long> form.  For
example, the two lines below are all equivalent:

    % fusioninventory-agent -s localhost
    % fusioninventory-agent --server-url localhost

=head2 Module selection options

=over

=item B<--list-modules>

List all available modules, modules planned for execution and exit

=item B<--no-module>=I<MODULE>

Do not run given module.

Multiple values can be specified, using comma as a separator. See option
I<--list-modules> for the list of available modules.

=back

=head2 Control server

=over

=item B<-s>, B<--server-url>=I<URI>

Use the given server as a controller for this agent. If protocol is not given, http is assumed.

=item B<-P>, B<--server-proxy>=I<PROXY>

Use I<PROXY> as HTTP proxy.

By default, the agent uses HTTP_PROXY environment variable.

=item B<-u> I<USER>, B<--server-user>=I<USER>

Use I<USER> for server authentication.

=item B<-p>, B<--server-password>=I<PASSWORD>

Use I<PASSWORD> for server authentication.

=item B<--server-ca-cert-path>=I<PATH>

CA certificates path, either a directory or a file.

=item B<--server-no-ssl-check>

Do not check server SSL certificate.

=item B<--server-timeout>=I<TIME>

Timeout for server connections.

=back

=head2 Web interface options

=over

=item B<--listener-disable>

Disable the embedded web server.

=item B<--listener-ip>=I<IP>

The network interface to use for the embedded web server (all).

=item B<--listener-port>=I<PORT>

The network port to use for the embedded web server (62354).

=item B<--listener-trust>=I<IP>

Trust requests from given addresses without authentication token (false).

For example: "192.168.0.0/24", "192.168.168.0.5" or an IP range like
"20.34.101.207 - 201.3.9.99". Hostnames are also accepted. See L<Net::IP>
documentation to get more example.

Multiple values can be specified, using comma as a separator.

=back

=head2 Logging options

=over

=item B<--logger-backend>=I<BACKEND>

Logger backend to use.

Multiple values can be specified, using comma as a separator. The available
backends are:

=over 4

=item

stderr: log messages directly in the console.

=item

file: log messages in a file.

=item

syslog: log messages through the local syslog server.

=back

Multiple values can be specified, using comma as a separator.

=item B<--logger-file>=I<FILE>

Log message in I<FILE> (implies File logger backend).

=item B<--logger-maxsize>=I<SIZE>

Max logfile size in MB, default is unlimited. When the max size is reached, the
file is truncated. This is only useful if there is no log rotation mechanism on
the system.

=item B<--logger-facility>=I<FACILITY>

Syslog facility to use (default LOG_USER).

=back

=head2 Configuration options

=over


=item B<--config-backend>=I<BACKEND>

Configuration backend to use.

The available backends are:

=over 4

=item

file: read configuration from a file (default anywhere else as Windows).

=item

registry: read configuration from the registry (default on Windows).

=item

none: don't read any configuration.

=back

=item B<--config-file>=I<FILE>

Use I<FILE> as configuration file (implies file configuration backend).

=item B<--config-reload-interval>=I<SECONDS>

SECONDS is the number of seconds between two configuration reloadings.
Default value is 0, which means that configuration is never reloaded.
Minimum value is 60. If given value is less than this minimum, it is set to
this minimum. If given value is less than 0, it is set to 0.

=back

=head2 Execution mode options

=over

=item B<-w> I<LIMIT>, B<--wait>=I<LIMIT>

Wait a random delay whose value is computed randomly between 0 and LIMIT
seconds, before execution. This is useful when execution is triggered from some
kind of system scheduling on multiple clients, to spread the server load.

=item B<-d>, B<--daemon>

Run the agent as a daemon.

=item B<--no-fork>

Don't fork in background.

This is only useful when running as a daemon.

=item B<--pidfile>=I<FILE>

Store pid in I<FILE>.

This is only useful when running as a daemon.

=item B<--tag>=I<TAG>

Add the given tag to every inventory results.

=item B<--debug>

Turn the debug mode on. You can use the parameter up to 3 times in a row
to increase the verbosity (e.g: B<--debug --debug>).

Level 3 turns on the debug mode of some external libraries like Net::SSLeay.
These messages will only be be printed on STDERR.

=item B<--setup>

Print the agent setup directories and exit.

=item B<--version>

Print the version and exit.

=back
