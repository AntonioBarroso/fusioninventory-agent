#!/usr/bin/perl

use strict;
use warnings;
use lib './lib';

use Cwd;
use English qw(-no_match_vars);
use File::Spec;
use Getopt::Long;
use Pod::Usage;
use Proc::Daemon;
use Proc::PID::File;

use FusionInventory::Agent;
use FusionInventory::Agent::Config;
use FusionInventory::Agent::Logger;

my %setup = (
    confdir => './etc',
    datadir => './share',
    libdir  => './lib',
    vardir  => './var',
);

my $options = {
    'conf-file' => $setup{confdir} . '/agent.cfg',
};

Getopt::Long::Configure( "no_ignorecase" );
GetOptions(
    $options,
    # executable options
    'conf-file=s',
    'config=s',
    'pidfile=s',
    'no-fork',
    'help|h',
    # agent configuration options
    'server|s=s',
    'tag|t=s',
    'http-ca-cert-dir=s',
    'http-ca-cert-file=s',
    'http-no-ssl-check',
    'http-password|p=s',
    'http-proxy|P=s',
    'http-timeout=i',
    'http-user|u=s',
    'httpd-disable',
    'httpd-ip=s',
    'httpd-port=s',
    'httpd-trust=s',
    'logger-backends=s',
    'logger-file=s',
    'logger-maxsize=i',
    'logger-verbosity=s',
) or pod2usage(-verbose => 0);

pod2usage(-verbose => 0, -exitstatus => 0) if $options->{help};

my $config = FusionInventory::Agent::Config->create(
    backend   => $options->{'config'},
    file      => $options->{'conf-file'},
    options   => {
        _ => {
            'server'       => $options->{'server'},
            'tag'          => $options->{'tag'},
        },
        http => {
            'ca_cert_dir'  => $options->{'http-ca-cert-dir'},
            'ca_cert_file' => $options->{'http-ca-cert-file'},
            'no_ssl_check' => $options->{'http-no-ssl-check'},
            'password'     => $options->{'http-password'},
            'proxy'        => $options->{'http-proxy'},
            'timeout'      => $options->{'http-timeout'},
            'user'         => $options->{'http-user'},
        }
        httpd => {
            'disable'      => $options->{'httpd-disable'},
            'ip'           => $options->{'httpd-ip'},
            'port'         => $options->{'httpd-port'},
            'trust'        => $options->{'httpd-trust'},
        }
        logger => {
            'backends'     => $options->{'logger-backends'},
            'file'         => $options->{'logger-file'},
            'maxsize'      => $options->{'logger-maxsize'}
            'verbosity'    => $options->{'logger-verbosity'},
        },
    }
);

my $logger = FusionInventory::Agent::Logger->new(
    backends  => $config->{logger}->{backends},
    file      => $config->{logger}->{logfile},
    maxsize   => $config->{logger}->{maxsize},
    facility  => $config->{logger}->{facility},
    verbosity => $config->{logger}->{verbosity},
);

my $agent = FusionInventory::Agent->new(
    config => $config,
    logger => $logger,
    setup  => \%setup
);

# initialize the agent
eval {
    $agent->init();
    $agent->initModules(fork => 1);
    $agent->initControllers();
    $agent->initHTTPInterface() unless $config->{httpd}->{'no-httpd'};
    $agent->initHandlers();
    die "no module available\n" unless $agent->getModules();
    die "no server defined\n" unless $agent->getControllers();

    my $pidfile  = $options->{pidfile} ||
                   $setup{vardir} . '/fusioninventory.pid';

    my $pid = Proc::PID::File->new();
    $pid->{path} = $pidfile;

    die "an agent is already running\n" if $pid->alive();

    if (!$options->{'no-fork'}) {

        # enforce absolute pathes, as daemonization changes working directory
        $pidfile = File::Spec->rel2abs($pidfile);
        foreach my $key (%{$agent->{setup}}) {
            $agent->{setup}->{$key} =
                File::Spec->rel2abs($agent->{setup}->{$key});
        }

        my $daemon = Proc::Daemon->new(
            pid_file => $pidfile
        );

        my $pid = $daemon->init();
        if ($pid) {
            $agent->{logger}->debug("agent daemonized");
            exit 0;
        }
    }

    $agent->{logger}->info("FusionInventory Agent starting");
};

if ($EVAL_ERROR) {
    $agent->{logger}->error("Initialization failure, aborting: $EVAL_ERROR");
    exit 1;
}

# run the agent endless loop
eval {
    $agent->run();
};

if ($EVAL_ERROR) {
    $agent->{logger}->error("Execution failure, exiting: $EVAL_ERROR");
    exit 1;
}

exit(0);

__END__

=head1 NAME

fusioninventory-daemon - FusionInventory agent daemon for Unix

=head1 SYNOPSIS

B<fusioninventory-daemon>
S<[B<--config> I<string>]>
S<[B<--conf-file> I<path>]>
S<[B<--no-fork>]>
S<[B<--pidfile> I<path>]>
S<[B<--server> I<url>]>
S<[B<--tag> I<string>]>
S<[B<--http-proxy> I<url>]>
S<[B<--http-user> I<string>]>
S<[B<--http-password> I<string>]>
S<[B<--http-ca-cert-dir> I<path>]>
S<[B<--http-ca-cert-file> I<path>]>
S<[B<--http-no-ssl-check>]>
S<[B<--http-timeout> I<integer>]>
S<[B<--logger-backends> I<string>]>
S<[B<--logger-file> I<path>]>
S<[B<--logger-maxsize> I<integer>]>
S<[B<--logger-verbosity> I<string>]>
S<[B<--httpd-disable>]>
S<[B<--httpd-ip> I<address>]>
S<[B<--httpd-port> I<integer>]>
S<[B<--httpd-trust> I<address>]>

=head1 DESCRIPTION

This executable allows to run the agent in managed mode on a Unix system: the
agent runs continuously in the background, wakes up periodically to contact
its server and execute required tasks. It may also be contacted out of schedule
by the server, through its web interface.

=head1 OPTIONS

=head2 Executable-specific options

Those options can be passed on command-line only.

=over

=item B<--config> I<string>

Configuration backend to use (default: file).

The available backends are:

=over 4

=item

file: read configuration from a file

=item

registry: read configuration from the registry

=item

none: don't read any configuration

=back

=item B<--conf-file> I<path>

configuration file path (with file backend)

=item B<--no-fork>

Don't fork in background.

=item B<--pidfile> I<path>

Store pid in I<path>.

=back

=head2 Agent configuration options

Those options can be passed on command-line to override values found in
configuration file.

=over

=item B<-s>, B<--server> I<url>

Set B<server> option, see L<agent.cfg> for details.

=item B<-t>, B<--tag> I<string>

Set B<tag> option, see L<agent.cfg> for details.

=item B<--http-proxy> I<url>

Set B<http/proxy> option, see L<agent.cfg> for details.

=item B<--http-user> I<string>

Set B<http/user> option, see L<agent.cfg> for details.

=item B<--http-password> I<string>

Set B<http/password> option, see L<agent.cfg> for details.

=item B<--http-ca-cert-dir> I<path>

Set B<http/ca-cert-dir> option, see L<agent.cfg> for details.

=item B<--http-ca-cert-file> I<path>

Set B<http/ca-cert-file> option, see L<agent.cfg> for details.

=item B<--http-no-ssl-check>

Set B<http/no-ssl-check> option, see L<agent.cfg> for details.

=item B<--http-timeout> I<integer>

Set B<http/timeout> option, see L<agent.cfg> for details.

=item B<--logger-backends> I<string>

Set B<logger/backends> option, see L<agent.cfg> for details.

=item B<--logger-file> I<path>

Set B<logger/file> option, see L<agent.cfg> for details.

=item B<--logger-maxsize> I<integer>

Set B<logger/maxsize> option, see L<agent.cfg> for details.

=item B<--logger-verbosity> I<string>

Set B<logger/verbosity> option, see L<agent.cfg> for details.

=item B<--httpd-disable>

Set B<httpd/disable> option, see L<agent.cfg> for details.

=item B<--httpd-ip> I<integer>

Set B<httpd/ip> option, see L<agent.cfg> for details.

=item B<--httpd-port> I<integer>

Set B<httpd/port> option, see L<agent.cfg> for details.

=item B<--httpd-trust> I<address>

Set B<httpd/trust> option, see L<agent.cfg> for details.

=back
