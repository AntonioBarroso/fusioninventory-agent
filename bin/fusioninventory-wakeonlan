#!/usr/bin/perl

use strict;
use warnings;
use lib './lib';

use English qw(-no_match_vars);
use Getopt::Long;
use Pod::Usage;

use FusionInventory::Agent;
use FusionInventory::Agent::Config;
use FusionInventory::Agent::Logger;
use FusionInventory::Agent::Task::WakeOnLan;
use FusionInventory::Agent::Tools::Network;

my %setup = (
    confdir => './etc',
    datadir => './share',
    libdir  => './lib',
    vardir  => './var',
);

my $options = {
    debug => 0,
};

GetOptions(
    $options,
    'ethernet!',
    'udp!',
    'debug+',
    'help',
) or pod2usage(-verbose => 0);

pod2usage(-verbose => 0, -exitval => 0) if $options->{help};

pod2usage(
    -message => "no mac address given, aborting\n", -verbose => 0
) unless @ARGV;

my $config = FusionInventory::Agent::Config->new(
    confdir => $setup{confdir},
    options => $options,
);

my $logger = FusionInventory::Agent::Logger->new(
    config => $config
);

my $agent = FusionInventory::Agent->new(
    config => $config,
    logger => $logger,
    setup  => \%setup
);
$agent->init();
$agent->initHandlers();

my $task = FusionInventory::Agent::Task::WakeOnLan->new(
    logger   => $logger,
    datadir  => $setup{datadir},
    confdir  => $setup{confdir},
    deviceid => 'foo',
);

$task->configure(
    addresses => [ @ARGV ],
    ethernet  => $options->{ethernet},
    udp       => $options->{udp}
);

$task->run();

__END__

=head1 NAME

fusioninventory-wakeonlan - Standalone wake-on-lan

=head1 SYNOPSIS

B<fusioninventory-wakeonlan>
S<[B<--ethernet>]> S<[B<--udp>]>
S<[B<--debug>]>
I<mac> S<[I<mac> ...]>

=head1 DESCRIPTION

F<fusioninventory-wakeonlan> allows to run a wakeonlan task without a GLPI
server.

=head1 OPTIONS

=over

=item B<--ethernet>

use ethernet method if available (true).

=item B<--udp>

use UDP method if available (true).

=item B<--debug>

debug output

=back

=head1 EXAMPLES

    $> fusioninventory-wakeonlan f0:1f:af:0f:a4:0c
    $> fusioninventory-wakeonlan f0:1f:af:0f:a4:0c --noethernet
    $> fusioninventory-wakeonlan f0:1f:af:0f:a4:0c f0:1f:af:0f:a4:0d
