#!/usr/bin/perl

use strict;
use warnings;
use lib './lib';

use English qw(-no_match_vars);
use Getopt::Long;
use Pod::Usage;

use FusionInventory::Agent;
use FusionInventory::Agent::Config;
use FusionInventory::Agent::Logger;
use FusionInventory::Agent::Task::WakeOnLan;
use FusionInventory::Agent::Tools::Network;

my %setup = (
    confdir => './etc',
    datadir => './share',
    libdir  => './lib',
    vardir  => './var',
);

my $options = {
    debug => 0,
};

GetOptions(
    $options,
    'mac=s',
    'methods=s',
    'debug+',
    'help',
) or pod2usage(-verbose => 0);

pod2usage(-verbose => 0, -exitval => 0) if $options->{help};
pod2usage(
    -message => "no mac address given, aborting\n", -verbose => 0
) unless $options->{mac};
pod2usage(
    -message => "invalid mac address given, aborting\n", -verbose => 0
) if $options->{mac} !~ /^$mac_address_pattern$/;

my $config = FusionInventory::Agent::Config->new(
    confdir => $setup{confdir},
    options => $options,
);

my $logger = FusionInventory::Agent::Logger->new(
    config => $config
);

my $agent = FusionInventory::Agent->new(
    config => $config,
    logger => $logger,
    setup  => \%setup
);
$agent->init();

my $task = FusionInventory::Agent::Task::WakeOnLan->new(
    logger   => $logger,
    datadir  => $setup{datadir},
    confdir  => $setup{confdir},
    deviceid => 'foo',
);

$task->configure(
    addresses => [ $options->{mac} ],
    methods   => [ $options->{methods} ? split(/,/, $options->{methods}) : () ]
);

$task->run();

__END__

=head1 NAME

fusioninventory-wakeonlan - Standalone wake-on-lan

=head1 SYNOPSIS

B<fusioninventory-wakeonlan>
S<[B<-mac> I<string>]> S<[B<--methods> I<string>]>
S<[B<--debug>]>

=head1 DESCRIPTION

F<fusioninventory-wakeonlan> allows to run a wakeonlan task without a GLPI
server.

=head1 OPTIONS

=over

=item B<--mac> I<string>

target mac address

=item B<--methods> I<string>

comma-separated list of methods to use (ethernet, udp)

=item B<--debug>

debug output

=back

=head1 EXAMPLES

    $> fusioninventory-wakeonlan --mac f0:1f:af:0f:a4:0c
    $> fusioninventory-wakeonlan --mac f0:1f:af:0f:a4:0c --methods udp
    $> fusioninventory-wakeonlan --mac f0:1f:af:0f:a4:0c,f0:1f:af:0f:a4:0d
