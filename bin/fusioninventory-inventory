#!/usr/bin/perl

use strict;
use warnings;
use lib './lib';

use English qw(-no_match_vars);
use Getopt::Long;
use File::Temp;
use Pod::Usage;

use FusionInventory::Agent;
use FusionInventory::Agent::Config;
use FusionInventory::Agent::Logger;
use FusionInventory::Agent::Target;
use FusionInventory::Agent::Task::Inventory;

my %setup = (
    confdir => './etc',
    datadir => './share',
    libdir  => './lib',
    vardir  => './var',
);

my $options = {
    debug  => 0,
    config => 'none'
};

GetOptions(
    $options,
    'directory=s',
    'server=s',
    'additional-content=s',
    'execution-timeout=s',
    'no-category=s@',
    'scan-homedirs',
    'scan-profiles',
    'tag|t=s',
    'verbose',
    'debug+',
    'help',
    'version',
    # deprecated options
    'backend-collect-timeout=s',
) or pod2usage(-verbose => 0);

if ($options->{version}) {
  print "Inventory task $FusionInventory::Agent::Task::Inventory::VERSION\n";
  exit 0;
}

pod2usage(-verbose => 0, -exitval => 0) if $options->{help};

my $config = FusionInventory::Agent::Config->new(
    confdir => $setup{confdir},
    options => $options,
);

my $logger = FusionInventory::Agent::Logger->new(
    config => $config
);

my $agent = FusionInventory::Agent->new(
    config => $config,
    logger => $logger,
    setup  => \%setup
);

my $task = FusionInventory::Agent::Task::Inventory->new(
    logger  => $logger,
    datadir => $setup{datadir},
    confdir => $setup{confdir},
);

$task->configure(
    tag                => $options->{tag},
    timeout            => $options->{'execution-timeout'},
    additional_content => $options->{'additional-content'},
    scan_homedirs      => $options->{'scan-homedirs'},
    no_category        => $options->{'no-category'},
);

my $target = FusionInventory::Agent::Target->create(
    url    => $options->{server},
    path   => $options->{directory},
    logger => $logger
);

$task->run(target => $target);

__END__

=head1 NAME

fusioninventory-inventory - Standalone inventory

=head1 SYNOPSIS

B<fusioninventory-inventory>
S<[B<--additional-content> I<file>]> S<[B<--no-category> I<string>]>
S<[B<--scan-homedirs>]> S<[B<--scan-profiles>]>
S<[B<--execution-timeout> I<integer>]> S<[B<--tag> I<string>]>
S<[B<--directory> I<path>]> S<[B<--server> I<url>]>
S<[B<--debug>]>

=head1 DESCRIPTION

F<fusioninventory-inventory> allows to run an inventory task without a GLPI
server.

=head1 OPTIONS

=over

=item B<--tag> I<string>

inventory tag

=item B<--execution-timeout> I<integer>

execution timeout (default: 30s)

=item B<--additional-content> I<file>

additional inventory content file

=item B<--no-category> I<string>

exclude specific inventory section

=item B<--scan-homedirs>

scan user home directories (false)

=item B<--scan-profiles>

scan user profiles (false)

=item B<--directory> I<path>

directory target path

=item B<--server> I<url>

server target url

=item B<--debug>

debug output

=back

=head1 EXAMPLES

    $> fusioninventory-inventory
    $> fusioninventory-inventory
        --directory /tmp
    $> fusioninventory-inventory
        --server http://glpi.domain.com/plugins/fusioninventory
    $> fusioninventory-inventory --no-category printers --no-category softwares
    $> fusioninventory-inventory --additional-content override.xml
